apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'io.fabric'

def static gitVersionCode() {
    def cmd = 'git rev-list HEAD --first-parent --count'
    cmd.execute().text.trim().toInteger()
}

android {

    signingConfigs {
        config {
            keyAlias rootProject.keyAlias
            keyPassword rootProject.keyPassword
            storeFile file('keystore/awaker.jks')
            storePassword rootProject.storePassword
        }
    }

    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "com.future.awaker"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        versionCode gitVersionCode()
        versionName "0.7.0"

        manifestPlaceholders = [
                fabric_apikey: rootProject.fabric_apikey
        ]

        multiDexEnabled true
    }

    buildTypes {

        debug {
            versionNameSuffix 'd'
            signingConfig signingConfigs.config
        }

        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def fileName = output.outputFile.name
            outputFileName = fileName.replace("app", "awaker-${variant.versionName}-${variant.versionCode}")
        }
    }
}

dependencies {
    def supportLibraryVersion = rootProject.supportLibraryVersion
    def retrofitVersion = '2.3.0'
    def glideVersion = '4.0.0'

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'com.android.support:multidex:1.0.2'


    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:support-annotations:$supportLibraryVersion"
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support:support-v13:$supportLibraryVersion"
    implementation "com.android.support:support-core-utils:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"

    // ViewModel and LiveData
    implementation "android.arch.lifecycle:runtime:1.0.3"
    implementation "android.arch.lifecycle:extensions:1.0.0"
    annotationProcessor "android.arch.lifecycle:compiler:1.0.0"

    // Room
    implementation "android.arch.persistence.room:runtime:1.0.0"
    annotationProcessor "android.arch.persistence.room:compiler:1.0.0"

    // Java8 support for Lifecycles
    implementation "android.arch.lifecycle:common-java8:1.0.0"

    // RxJava support for Room
    implementation "android.arch.persistence.room:rxjava2:1.0.0"

    // ReactiveStreams support for LiveData
    implementation "android.arch.lifecycle:reactivestreams:1.0.0"

    //retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

    //rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.6'

    //glide
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$glideVersion"

    // core ui
    implementation 'com.gordonwong:material-sheet-fab:1.2.1'

    implementation 'com.pnikosis:materialish-progress:1.1'
    implementation 'net.soulwolf.widget:ratiolayout:1.0.0'
    implementation 'com.github.chrisbanes:PhotoView:2.0.0'
    implementation 'com.just.agentweb:agentweb:1.2.6'
    implementation 'org.jsoup:jsoup:1.10.3'
    implementation 'com.afollestad.material-dialogs:core:0.9.4.6'

    implementation 'com.sun.mail:android-mail:1.5.5'
    implementation 'com.sun.mail:android-activation:1.5.5'

    implementation 'com.blankj:utilcode:1.9.0'

    implementation('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }
}
